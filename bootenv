setenv ipaddr 192.168.0.102
setenv netmask 255.255.255.0
setenv gatewayip 192.168.0.254
setenv serverip 192.168.0.80
setenv tftpdir bamr


setenv bootbin BOOT.BIN
setenv fitimagefile image.ub
setenv jffsfile rootfs.jffs2

setenv bootfile uboot.bin
setenv bootfile_offset 0x00000000
setenv bootfile_size 0x00200000

setenv bootenvfile uboot.env
setenv bootenvfile_offset 0x00260000
setenv bootenvfile_size 0x00020000

setenv bootscrfile boot.scr
setenv bootscrfile_offset 0x00200000
setenv bootscrfile_size 0x00020000
setenv script_size_f ${bootscrfile_size}

setenv dtbfile system.dtb
setenv dtbfile_offset 0x00220000
setenv dtbfile_size 0x00040000

setenv plfile bamr_mdm_v5.49.bin
setenv plfile_offset 0x00280000
setenv plfile_size 0x02000000

setenv kernelfile Image.gz
setenv kernelfile_offset 0x02280000
setenv kernelfile_size 0x00A00000

setenv ramfsfile rootfs.cpio.gz.u-boot
setenv ramfsfile_offset 0x02C80000
setenv ramfsfile_size 0x00F00000

setenv sparefile spare.jffs2
setenv sparefile_offset 0x03B80000
setenv sparefile_size 0x04480000


setenv bootcmd           'run distro_bootcmd'
setenv boot_targets      'mmc0 qspi0'
setenv distro_bootcmd    'for target in ${boot_targets}; do echo; echo Load boot images from ${target}; run bootcmd_${target}; done'

setenv bootargs_ram      'setenv bootargs earlycon console=ttyPS0,115200 clk_ignore_unused root=/dev/ram rw rootwait'
setenv bootargs_mmc      'setenv bootargs earlycon console=ttyPS0,115200 clk_ignore_unused root=/dev/mmcblk0p2 rw rootwait'
setenv bootfrom_sf       'sf probe 0 0 0; sf read ${loadaddr} ${plfile_offset} ${plfile_size}; fpga load 0 ${loadaddr} ${plfile_size}; sf read 0x100000 ${dtbfile_offset} ${dtbfile_size}; sf read 0x200000 ${kernelfile_offset} ${kernelfile_size}; sf read 0x4000000 ${ramfsfile_offset} ${ramfsfile_size}'
setenv bootfrom_tftp     'tftpboot ${loadaddr} ${tftpdir}/${plfile}; fpga load 0 ${loadaddr} ${filesize}; tftpboot 0x100000 ${tftpdir}/${dtbfile}; tftpboot 0x200000 ${tftpdir}/${kernelfile}; tftpboot 0x4000000 ${tftpdir}/${ramfsfile}'

setenv bootcmd_qspi0     'run bootargs_ram; run bootfrom_sf; booti 0x200000 0x4000000 0x100000'
setenv bootcmd_mmc0      'run bootargs_mmc; run bootfrom_sf; booti 0x200000 - 0x100000'

setenv bootcmd_qspi0_net 'run bootargs_ram; run bootfrom_tftp; booti 0x200000 0x4000000 0x100000'
setenv bootcmd_mmc0_net  'run bootargs_mmc; run bootfrom_tftp; booti 0x200000 - 0x100000'


setenv updatecmd         'sf probe 0 0 0; sf erase ${targetfile_offset} ${targetfile_size}; tftpboot 0x100000 ${tftpdir}/${targetfile}; sf write 0x100000 ${targetfile_offset} ${filesize}; run updatesize'
setenv uploadcmd         'sf probe 0 0 0; sf read 0x100000 ${targetfile_offset} ${targetfile_size}; tftpput 0x100000 ${targetfile_size} ${serverip}:${tftpdir}/new_${targetfile}'
setenv updatesize        'setenv ${targetname}_size ${filesize}; saveenv'

setenv update_targets    'boot dtb pl kernel ramfs'
setenv update_all        'for target in ${update_targets}; do echo; echo Update an image for ${target}; run update_${target}; done'
setenv update_boot       'setenv targetname bootfile; setenv targetfile ${bootfile}; setenv targetfile_offset ${bootfile_offset}; setenv targetfile_size ${bootfile_size}; run updatecmd'
setenv update_bootenv    'setenv targetname bootenvfile; setenv targetfile ${bootenvfile}; setenv targetfile_offset ${bootenvfile_offset}; setenv targetfile_size ${bootenvfile_size}; run updatecmd'
setenv update_bootscr    'setenv targetname bootscrfile; setenv targetfile ${bootscrfile}; setenv targetfile_offset ${bootscrfile_offset}; setenv targetfile_size ${bootscrfile_size}; run updatecmd'
setenv update_dtb        'setenv targetname dtbfile; setenv targetfile ${dtbfile}; setenv targetfile_offset ${dtbfile_offset}; setenv targetfile_size ${dtbfile_size}; run updatecmd'
setenv update_pl         'setenv targetname plfile; setenv targetfile ${plfile}; setenv targetfile_offset ${plfile_offset}; setenv targetfile_size ${plfile_size}; run updatecmd'
setenv update_kernel     'setenv targetname kernelfile; setenv targetfile ${kernelfile}; setenv targetfile_offset ${kernelfile_offset}; setenv targetfile_size ${kernelfile_size}; run updatecmd'
setenv update_ramfs      'setenv targetname ramfsfile; setenv targetfile ${ramfsfile}; setenv targetfile_offset ${ramfsfile_offset}; setenv targetfile_size ${ramfsfile_size}; run updatecmd'
setenv update_spare      'setenv targetname sparefile; setenv targetfile ${sparefile}; setenv targetfile_offset ${sparefile_offset}; setenv targetfile_size ${sparefile_size}; run updatecmd'

setenv upload_all        'setenv targetname bootbin; setenv targetfile ${bootbin}; setenv targetfile_offset 0x0; setenv targetfile_size 0x8000000; run uploadcmd'
setenv upload_boot       'setenv targetname bootfile; setenv targetfile ${bootfile}; setenv targetfile_offset ${bootfile_offset}; setenv targetfile_size ${bootfile_size}; run uploadcmd'
setenv upload_bootenv    'setenv targetname bootenvfile; setenv targetfile ${bootenvfile}; setenv targetfile_offset ${bootenvfile_offset}; setenv targetfile_size ${bootenvfile_size}; run uploadcmd'
setenv upload_spare      'setenv targetname sparefile; setenv targetfile ${sparefile}; setenv targetfile_offset ${sparefile_offset}; setenv targetfile_size ${sparefile_size}; run uploadcmd'
